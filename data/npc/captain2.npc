# GIMUD - Graphical Interface Multi User Dungeon
# captain2.npc: Kapitän Greyhound in Carlin

Name = "Captain Greyhound"
Outfit = (129,96-113-95-115)
Home = [32388,31822,6]
Radius = 2

Behaviour = {
ADDRESS,"hello$",male,!   -> "Welcome on board, Sir %N."
ADDRESS,"hi$",male,!      -> *
ADDRESS,"hello$",female,! -> "Welcome on board, Madam %N."
ADDRESS,"hi$",female,!    -> *
ADDRESS,!                 -> Idle
BUSY,"hello$",!           -> "One moment please %N. You're next in line.", Queue
BUSY,"hi$",!              -> *
BUSY,!                    -> NOP
VANISH,!                  -> "Good bye. Recommend us, if you were satisfied with our service."

"bye"          -> "Good bye. Recommend us, if you were satisfied with our service.", Idle
"farewell"     -> *
"name"         -> "My name is Captain Greyhound from the Royal Tibia Line."
"job"          -> "I am the captain of this sailing-ship."
"captain"      -> *
"ship"         -> "The Royal Tibia Line connects all seaside towns of Tibia."
"line"         -> *
"company"      -> *
"route"        -> *
"tibia"        -> *
"good"         -> "We can transport everything you want."
"passenger"    -> "We would like to welcome you on board."
"trip"         -> "Where do you want to go? To Thais, Ab'Dendriel, Venore or Edron?"
"passage"      -> *
"town"         -> *
"destination"  -> *
"sail"         -> *
"go"           -> *
"ice"          -> "I'm sorry, but we don't serve the routes to the Ice Islands."
"senja"        -> *
"folda"        -> *
"vega"         -> *
"darashia"     -> "I'm not sailing there. This route is afflicted by a ghost ship! However I've heard that Captain Fearless from Venore sails there."
"darama"       -> *
"ghost"        -> "Many people who sailed to Darashia never returned because they were attacked by a ghostship! I'll never sail there!"

"thais"       -> Price=110, "Do you seek a passage to Thais for %P gold?", Topic=1
"carlin"      -> "This is Carlin. Where do you want to go?"
"ab'dendriel" -> Price=80,  "Do you seek a passage to Ab'Dendriel for %P gold?", Topic=3
"edron"       -> Price=110, "Do you seek a passage to Edron for %P gold?", Topic=4
"venore"      -> Price=130, "Do you seek a passage to Venore for %P gold?", Topic=5

"thais",QuestValue(250)>2       -> Price=100, "Do you seek a passage to Thais for %P gold?", Topic=1
"ab'dendriel",QuestValue(250)>2 -> Price=70,  "Do you seek a passage to Ab'Dendriel for %P gold?", Topic=3
"edron",QuestValue(250)>2       -> Price=100, "Do you seek a passage to Edron for %P gold?", Topic=4
"venore",QuestValue(250)>2      -> Price=120, "Do you seek a passage to Venore for %P gold?", Topic=5

Topic=1,"yes",PZBlock,! -> "First get rid of those blood stains! You are not going to ruin my vehicle!"

Topic=3,"yes",PZBlock,! -> *
Topic=4,"yes",PZBlock,! -> *
Topic=5,"yes",PZBlock,! -> *


Topic=1,"yes",Premium,CountMoney>=Price -> "Set the sails!", DeleteMoney, Idle, EffectOpp(11), Teleport(32312,32211,6), EffectOpp(11)
Topic=3,"yes",Premium,CountMoney>=Price -> "Set the sails!", DeleteMoney, Idle, EffectOpp(11), Teleport(32733,31668,6), EffectOpp(11)
Topic=4,"yes",Premium,CountMoney>=Price -> "Set the sails!", DeleteMoney, Idle, EffectOpp(11), Teleport(33175,31764,6), EffectOpp(11)
Topic=5,"yes",Premium,CountMoney>=Price -> "Set the sails!", DeleteMoney, Idle, EffectOpp(11), Teleport(32954,32023,6), EffectOpp(11)
Topic>0,"yes",CountMoney>=Price         -> "I'm sorry, but you need a premium account in order to travel onboard our ships."
Topic>0,"yes"                           -> "You don't have enough money."
Topic>0                                 -> "We would like to serve you some time."
}
