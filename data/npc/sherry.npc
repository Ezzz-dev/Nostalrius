# GIMUD - Graphical Interface Multi User Dungeon
# sherry.npc: Datenbank für die Bäuerin Sherry McRonald

Name = "Sherry McRonald"
Outfit = (136,78-94-38-58)
Home = [32390,32241,7]
Radius = 2

Behaviour = {
ADDRESS,"hello$",! -> "Hello %N! Welcome to our humble farm."
ADDRESS,"hi$",!    -> *
ADDRESS,!          -> Idle
BUSY,"hello$",!    -> "Please wait a minute, %N.", Queue
BUSY,"hi$",!       -> *
BUSY,!             -> NOP
VANISH,!           -> "What a strange person."

"bye"         -> "Grace our home with another visit soon.", Idle
"farewell"    -> *
"job"         -> "I and my husband run this farm."
"husband"     -> "My husband Donald is busy on the fields almost all night and day."
"donald"      -> *
"farm"        -> "It is a hard work, but the city needs us."
"name"        -> "I am Sherry McRonald."
"time"        -> "Sorry, I don't have a watch."
"weather"     -> "The weather is the best friend and the worst enemy of a farmer."
"field"       -> "The druids helped us by placing a blessing on our fields."
"city"        -> "The city needs our crops."
"crops"       -> "It's hard to harvest it, carry it to the mill in the north and make flour. If you can bake some bread I will buy it for 2 gold."
"mill"        -> "The miller is a lazy fellow and afraid of his own mill, because he thinks it is spooked."
"spooked"     -> "I don't know for sure. The miller claims that his mill is threatened by some monsters sometimes."
"king"        -> "King Tibianus granted us this farm to earn a living."
"tibianus"    -> *
"frodo"       -> "He is a friend of my husband."
"oswald"      -> "This lazy fellow has nothing better to do than to spread rumours."
"bloodblade"  -> "He is an impressive warrior as far as I can tell."
"muriel"      -> "We a mere peasants and don't know much about the guild leaders."
"elane"       -> *
"gregor"      -> *
"marvik"      -> *
"gorn"        -> "He doesn't talk much to us."
"sam"         -> "He is too busy to care much about farmers like us."
"quentin"     -> "What a nice person he is."
"lynda"       -> "She is sooo charming. I can't believe she is not married yet! Have you met her?", Topic=2
"spider"      -> "Spiders infested the sewers beneath our farm. We need some help to exterminate them. My husband pays a reward for killed spiders."
"monster"     -> *
"help"        -> *

"buy"             -> "I can offer you cheese, cherries, and melons."
"do","you","sell" -> *
"do","you","have" -> *
"food"            -> "Are you looking for food? I have cheese, cherries, pumpkins and melons."

"cheese" -> Type=3607, Amount=1, Price=5, "Do you want to buy cheese for %P gold?", Topic=1
"cherry" -> Type=3590, Amount=1, Price=1, "Do you want to buy a cherry for %P gold?", Topic=1
"melon"  -> Type=3593, Amount=1, Price=8, "Do you want to buy a melon for %P gold?", Topic=1
"pumpkin" -> Type=3594, Amount=1, Price=10, "Do you want to buy a pumpkin for %P gold?", Topic=1

%1,1<%1,"cheese" -> Type=3607, Amount=%1, Price=5*%1, "Do you want to buy %A cheeses for %P gold?", Topic=1
%1,1<%1,"cherr"  -> Type=3590, Amount=%1, Price=1*%1, "Do you want to buy %A cherries for %P gold?", Topic=1
%1,1<%1,"melon"  -> Type=3593, Amount=%1, Price=8*%1, "Do you want to buy %A melons for %P gold?", Topic=1
%1,1<%1,"pumpkin" -> Type=3594, Amount=%1, Price=10*%1, "Do you want to buy %A pumpkins for %P gold?", Topic=1


"sell","bread"  -> "I will pay 2 gold for every bread, is that ok?", Topic=3

Topic=1,"yes",CountMoney>=Price -> "Here it is.", DeleteMoney, Create(Type)
Topic=1,"yes"                   -> "I am sorry, but you do not have enough gold."
Topic=1                         -> "Maybe later."

Topic=2,"yes",male    -> "She really should find a husband with ease! You should ask her for a date."
Topic=2,"yes",female  -> "She really should find a husband with ease! If you know a bachelor, introduce him to her."
Topic=2               -> "Oh, if you say so."

Topic=3,"yes",Count(3600)>0 -> Amount=Count(3600), Price=Amount*2, "Here you are ... %P gold.", Delete(3600), CreateMoney
Topic=3,"yes"                -> "Sorry, you don't have any bread."
Topic=3                      -> "Maybe another time."
}
