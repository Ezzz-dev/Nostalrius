# GIMUD - Graphical Interface Multi User Dungeon
# uzon: Datenbank für den Teppichpiloten Uzon auf den Femor Hills

Name = "Uzon"
Outfit = (130,95-5-18-76)
Home = [32537,31836,4]
Radius = 1

Behaviour = {
ADDRESS,"hello$",!         -> "Daraman's blessings, traveller %N."
ADDRESS,"hi$",!            -> *
ADDRESS,!                  -> Idle
BUSY,"hello$",!            -> "Hastiness is not the way of the people of Darama, %N. Give me the time I need here.", Queue
BUSY,"hi$",!               -> *
BUSY,!                     -> NOP
VANISH,!                   -> "Daraman's blessings."

"bye"             -> "Daraman's blessings", Idle
"name"            -> "I am known as Uzon Ibn Kalith."
"job"             -> "I am a licensed Darashian carpetpilot. I can bring you to Darashia or Edron."
"time"            -> "It's %T right now. The next flight is scheduled soon."
"caliph"          -> "The caliph welcomes travellers to his land."
"kazzan"          -> *
"daraman"         -> "Oh, there is so much to tell about Daraman. You better travel to Darama to learn about his teachings."
"ferumbras"       -> "I would never transport this one."
"drefia"          -> "So you heared about haunted Drefia? Many adventures travel there to test their skills against the undead: vampires, mummies, and ghosts." 
"excalibug"       -> "Some people claim it is hidden somewhere under the endless sands of the devourer desert in Darama."
"thais"           -> "Thais is noisy and overcroweded. That's why I like Darashia more."
"tibia"           -> "I have seen almost every place on the continent."
"continent"       -> "I could retell the tales of my travels for hours. Sadly another flight is scheduled soon."
"carlin"          -> "Just another Thais but with women to lead them."
"flying","carpet" -> "You can buy flying carpets only in Darashia."
"fly"             -> "I transport travellers to the continent of Darama for a small fee. So many want to see the wonders of the desert and learn the secrets of Darama."
"news"            -> "I heard too many news to recall them all."
"rumors"          -> *

"passage"   -> "I can fly you to Darashia on Darama or Edron if you like. Where do you want to go?"
"transport" -> *
"ride"      -> *
"trip"      -> *

"darashia"  -> Price=60, "Do you want to get a ride to Darashia on Darama for %P gold?", Topic=1
"darama"    -> *
"edron"     -> Price=60, "Do you want to get a ride to Edron for %P gold?", Topic=2

"darashia",QuestValue(250)>2  -> Price=50, "Do you want to get a ride to Darashia on Darama for %P gold?", Topic=1
"darama",QuestValue(250)>2    -> *
"edron",QuestValue(250)>2     -> Price=50, "Do you want to get a ride to Edron for %P gold?", Topic=2

Topic=1,"yes",PZBlock,! -> "First get rid of those blood stains! You are not going to ruin my vehicle!"
Topic=2,"yes",PZBlock,! -> *

Topic=1,"yes",Premium,CountMoney>=Price -> "Hold on!", DeleteMoney, Idle, EffectOpp(11), Teleport(33269,32441,6), EffectOpp(11)
Topic=1,"yes",CountMoney>=Price         -> "I'm sorry, but you need a premium account in order to enter Darama."
Topic=1,"yes"                           -> "You don't have enough money."
Topic=1                                 -> "You shouldn't miss the experience."

# für postquest
Topic=2,"yes",Premium, QuestValue(227)=2,CountMoney>=Price -> "Hold on!", DeleteMoney, Idle, EffectOpp(11), Teleport(33193,31784,3), EffectOpp(11),SetQuestValue(227,3)


Topic=2,"yes",Premium,CountMoney>=Price -> "Hold on!", DeleteMoney, Idle, EffectOpp(11), Teleport(33193,31784,3), EffectOpp(11)
Topic=2,"yes",CountMoney>=Price         -> "I'm sorry, but you need a premium account in order to enter Edron."
Topic=2,"yes"                           -> "You don't have enough money."
Topic=2                                 -> "You shouldn't miss the experience."
}
