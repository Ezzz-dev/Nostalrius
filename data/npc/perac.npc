# GIMUD - Graphical Interface Multi User Dungeon
# perac.npc: Datenbank für den Bogner Perac

Name = "Perac"
Outfit = (129,78-52-68-114)
Home = [32295,31784,7]
Radius = 1

Behaviour = {
ADDRESS,"hello$",! -> "Oh, please come in. What do you need?"
ADDRESS,"hi$",!    -> *
ADDRESS,!          -> Idle
BUSY,"hello$",!    -> "Sorry, I am already talking to a customer."
BUSY,"hi$",!       -> *
BUSY,!             -> NOP
VANISH,!           -> "Good bye."

"bye"             -> "Good bye.", Idle
"farewell"        -> *
"job"             -> "I am the fletcher of Carlin. I am selling bows, crossbows, and ammunition. Do you need anything?"
"name"            -> "I am Perac, fletcher and marksman extraordinaire."
"marksman"        -> "I am a paladin and the best marksman in the land."
"time"            -> "Don't bother me. Go and buy a watch."
"ghostlands"      -> "I was there ... once. I got out before the illusions drove me mad. Better stay out of that area!"


"bow"             -> Type=3350, Amount=1,  Price=400,  "Do you want to buy a bow for %P gold?", Topic=1
"crossbow"        -> Type=3349, Amount=1,  Price=500,  "Do you want to buy a crossbow for %P gold?", Topic=1
"arrow"           -> Type=3447, Amount=1, Price=2,   "Do you want to buy an arrow for %P gold?", Topic=1
"bolt"            -> Type=3446, Amount=1, Price=3,   "Do you want to buy a bolt for %P gold?", Topic=1

%1,1<%1,"bow"             -> Type=3350, Amount=%1,  Price=400*%1,  "Do you want to buy %A bows for %P gold?", Topic=1
%1,1<%1,"crossbow"        -> Type=3349, Amount=%1,  Price=500*%1,  "Do you want to buy %A crossbows for %P gold?", Topic=1
%1,1<%1,"arrow"           -> Type=3447, Amount=%1, Price=2*%1,   "Do you want to buy %A arrows for %P gold?", Topic=1
%1,1<%1,"bolt"            -> Type=3446, Amount=%1, Price=3*%1,   "Do you want to buy %A bolts for %P gold?", Topic=1

"buy"             -> "I am selling bows, crossbows, and ammunition. Do you need anything?"
"do","you","sell" -> *
"do","you","have" -> *
"offer"           -> *
"goods"           -> *
"ammo"            -> "Do you need arrows for a bow or bolts for a crossbow?"
"ammunition"      -> *

Topic=1,"yes",CountMoney>=Price -> "Here you are.", DeleteMoney, Create(Type)
Topic=1,"yes"                   -> "Come back, when you have enough money."
Topic=1                         -> "Hmm, but next time."
}
